manifest {
    name = 'crukci-bioinformatics/nf-alignment'
    author = 'Richard Bowers'
    homePage = 'https://github.com/crukci-bioinformatics/nf-alignment'
    description = 'Alignment (including sample merging) for CRUK-CI use only.'
    mainScript = 'alignment.nf'
    nextflowVersion = '>=23.9.0'
    recurseSubmodules = true
    version = '4.3.1'
}

executor {
    $slurm {
        queueSize = 200
        pollInterval = '30sec'
        clusterOptions = "--nodes=1 --open-mode=truncate"
    }
}

singularity.enabled = true
singularity.autoMounts = true
singularity.runOptions = "--no-home -B '${projectDir}'"

clean = true

process {
    container = "crukcibioinformatics/alignment:${manifest.version}"

    errorStrategy = {
        task.exitStatus in [ 104, 134, 137..140, 255 ].flatten() ? 'retry' : 'finish'
    }

    cpus = 1
    memory = 1.GB
    time = 1.hour

    withLabel:bwa {
        memory = { 4.GB * task.attempt }
        time = { 2.hour + 2.hour * task.attempt }
        maxRetries = 2
    }

    withLabel:picard {
       memory = { 2.GB * 2 ** task.attempt } // So 4GB, 8, 16, 32
       time = { 4.hour * task.attempt }
       maxRetries = 3
    }

    withLabel:picardSmall {
       memory = { 256.MB * 2 ** task.attempt } // 512MB, 1G, 2G
       time = { 1.hour * task.attempt }
       maxRetries = 2
    }

    withLabel:metrics {
        errorStrategy = {
            task.exitStatus in [ 104, 134, 137..140 ].flatten() ? 'retry' : 'ignore'
        }
    }

    withLabel:coverage {
        memory = { 2.GB * task.attempt }
        time = { 2.hour * task.attempt }
        maxRetries = 2
    }
}

params {
    // Defaults if not defined otherwise in "nextflow.config".
    markDuplicates = true
    mergeSamples = false
    createCoverage = false

    // Size of chunks (blocks of FASTQ reads).
    // Note that increasing this significantly may impact the memory required for some
    // tasks after alignment, which can be done in a local nextflow.config file.
    chunkSize = 10000000

    // Additional metrics.
    alignmentMetrics = true
    insertSizeMetrics = true
    wgsMetrics = false
    rnaseqMetrics = false

    // Metrics specific options.

    // CollectRNASeqMetrics STRAND_SPECIFICITY.
    // For strand-specific library prep. For unpaired reads, use FIRST_READ_TRANSCRIPTION_STRAND
    // if the reads are expected to be on the transcription strand.
    // Possible values: {NONE, FIRST_READ_TRANSCRIPTION_STRAND, SECOND_READ_TRANSCRIPTION_STRAND}
    // The checking code at the start of the pipeline sets the default according to
    // single read or paired end if it's not explicitly set in the user's nextflow.config.

    rnaseqStrandSpecificity = ''

    // Additional configuration for aligners.
    // We run with standard options. For more control, change these values to include
    // other flags. Some flags are used already, so don't set these twice.
    // Obviously this includes the options that provide the input files, the reference path
    // and where available the number of threads/cores.
    // BWA has the "aln" step and the "samse/sampe" step, so has two parameters for these.
    // STAR has a few options set already: check in templates/STAR.sh

    bowtie2Options = ''
    bwaAlnOptions = ''
    bwaSamOptions = ''
    bwamem2Options = ''
    starOptions = ''

    // Folder structure and file names.

    // The default pattern and capture group for identifying a base name from CRUK-CI
    // FASTQ files. If any other file naming pattern is used, this will be set in
    // nextflow.config.
    basenameExtractPattern = /(SLX-\d+\.\w+\.[-\w]+\.s_\d)\.r_\d+\.fq\.gz/
    basenameCaptureGroup = 1

    // Directory for FASTQ files. The default is "fastq" in the pipeline directory.
    fastqDir = "${launchDir}/fastq"

    // Directories for aligned files and merged sample BAMs.
    bamDir = "${launchDir}/bam"
    sampleBamDir = "${launchDir}/samplebam"

    // Default name for the driver CSV file.
    alignmentCSV = "${launchDir}/alignment.csv"

    // Software versions. Used in the paths of standard reference genomes.
    bowtie2Version = '2.5.1'
    bwaVersion = '0.7.17'
    bwamem2Version = '2.2.1'
    starVersion = '2.7.10b'

    // Need a little more memory overhead for the Java tasks.
    java_metaspace_size = 256
    java_overhead_size = 128
}

profiles {
    standard {
        if (!params.referenceRoot) {
            params.referenceRoot = '/mnt/scratchc/bioinformatics/reference_data/reference_genomes'
        }
        process.executor = 'local'
        executor.$local.cpus = 6
        executor.$local.memory = 20.GB
    }

    epyc {
        if (!params.referenceRoot) {
            params.referenceRoot = '/mnt/scratchc/bioinformatics/reference_data/reference_genomes'
        }
        process.executor = 'slurm'
        process.queue = 'epyc'
    }

    bioinf {
        if (!params.referenceRoot) {
            params.referenceRoot = '/mnt/nas-data/bioinformatics/shared_folders/reference_data/2023/reference_genomes'
        }
        process.executor = 'local'
        executor.$local.cpus = 28
        executor.$local.memory = 180.GB
    }
}


// https://www.nextflow.io/docs/latest/tracing.html
timeline {
    enabled = true
    overwrite = true
    file = "reports/execution_timeline.html"
}
report {
    enabled = true
    overwrite = true
    file = "reports/execution_report.html"
}
trace {
    enabled = false
    overwrite = true
    file = "reports/execution_trace.txt"
}
dag {
    enabled = false
    overwrite = true
    file = "reports/pipeline_dag.svg"
}
